;; Q: The idea of smoothing a function is an important concept in signal
;; processing. If f is a function and dx is some small number, then the
;; smoothed version of f is the function whose value at a point x is the
;; average of f(x - dx), f(x), and f(x + dx). Write a procedure smooth that
;; takes as input a procedure that computes f and returns a procedure that
;; computes the smoothed f. It is sometimes valuable to repeatedly smooth a
;; function (that is, smooth the smoothed function, and so on) to obtained the
;; n-fold smoothed function. Show how to generate the n-fold smoothed function
;; of any given function using smooth and repeated from exercise 1.43.

(define (compose f g)
  (lambda (x)
    (f (g x))))

(define (repeated f n)
  (define (repeated-iter ff m)
    (if (= m 1)
        ff
        (repeated-iter (compose f ff) (- m 1))))
  (trace repeated-iter)
  (repeated-iter f n))

;; A:

(define dx 0.00001)

(define (smooth f)
  (lambda (x)
    (/ (+
         (f (- x dx))
         (f x)
         (f (+ x dx)))
       3)))

;; To generate the n-fold smoothed function for any given function, we need to
;; only repeat the smoothing, not duplicate the original function.

(define (n-fold-smooth f n)
    ((repeated smooth n) f))

;; It should be noted that this will create a procedure with steps O(3^n)!
